apply plugin: 'com.android.application'
apply plugin: 'co.doppl.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "co.doppl.so"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def supportVersion = "26.1.0"
def archVer = "1.0.0-rc1"
def retroVer = "2.3.0"

def dopplArchVer = "1.0.0.0-rc1"
def dopplRetroVer = "2.3.0.7"

dependencies {
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation("com.android.support:support-v4:$supportVersion") {
        exclude group: 'com.android.support', module: 'support-media-compat'
    } // for https://issuetracker.google.com/issues/64909326
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "android.arch.lifecycle:reactivestreams:$archVer"

    implementation "io.reactivex.rxjava2:rxjava:2.1.5"
    doppl "co.doppl.io.reactivex.rxjava2:rxjava:2.1.5.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    doppl "co.doppl.io.reactivex.rxjava2:rxandroid:2.0.1.2"

    implementation "com.squareup.retrofit2:retrofit:$retroVer"
    implementation "com.squareup.retrofit2:converter-gson:$retroVer"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retroVer"
    doppl "co.doppl.com.squareup.retrofit2.urlsession:retrofit:$dopplRetroVer"
    doppl "co.doppl.com.squareup.retrofit2.urlsession:converter-gson:$dopplRetroVer"
    doppl "co.doppl.com.squareup.retrofit2.urlsession:adapter-rxjava2:$dopplRetroVer"

    implementation "android.arch.lifecycle:runtime:1.0.3"
    doppl "co.doppl.android.arch.lifecycle:runtime:$dopplArchVer"
    implementation "android.arch.lifecycle:extensions:$archVer"
    doppl "co.doppl.android.arch.lifecycle:extensions:$dopplArchVer"
    annotationProcessor "android.arch.lifecycle:compiler:$archVer"

    testImplementation "junit:junit:4.12"
    testImplementation "co.doppl.lib:androidbasetest:0.8.5"
    testDoppl "co.doppl.lib:androidbasetest:0.8.5.0"
}

dopplConfig {
    translatePattern {
        include 'co/doppl/so/api/**'
        include 'co/doppl/so/arch/**'
        include 'co/doppl/so/RepositoryTest.java'
    }

    translatedPathPrefix 'co.doppl.so.api', 'SOAPI'
    translatedPathPrefix 'co.doppl.so.arch', 'SOA'

    testIdentifier {
        include 'co/doppl/so/RepositoryTest.java'
    }
}
